plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
}

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url = "https://libraries.minecraft.net" }
}

configurations {
    create("testing")
    implementation.extendsFrom(testing)
}

dependencies {
    compileOnly(project(":common"))

    // We create a module that contains type definitions
    // of the paper API but no implementations. This will allow us to
    // create valid code that calls Paper APIs without having to depend
    // on Paper directly, since Paper will require us to compile
    // on Java 17.
    compileOnly(project(":paper-types"))

    /* Spigot 1.13.2 */
    compileOnly("org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT")

    /* For adventure components support */
    compileOnly("net.kyori:adventure-platform-bukkit:4.1.2")

    /* For Brigadier on 1.13+ */
    compileOnly("com.mojang:brigadier:1.0.18")

    /* For Kotlin extensions */
    compileOnly("org.jetbrains.kotlin:kotlin-stdlib")
}

compileJava {
    options.compilerArgs += ["-parameters"]
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.testing.asFileTree.files.collect { zipTree(it) }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
